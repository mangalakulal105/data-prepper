/*
 *  SPDX-License-Identifier: Apache-2.0
 *
 *  The OpenSearch Contributors require contributions made to
 *  this file be licensed under the Apache-2.0 license or a
 *  compatible open source license.
 *
 *  Modifications Copyright OpenSearch Contributors. See
 *  GitHub history for details.
 */
plugins {
    id 'com.diffplug.spotless' version '5.12.4'
}

apply from: file("${rootDir}/build-resources.gradle")
allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'com.diffplug.spotless'

    group = 'com.amazon'
    
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    spotless {
        format 'markdown', {
            target '*.md'
            // TODO: enrich format rules
            endWithNewline()
        }
        format 'misc', {
            target '.gitignore', '*.yml', '*.yaml'
            // TODO: enrich format rules
            trimTrailingWhitespace()
            endWithNewline()
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    sourceCompatibility = '1.8'
    spotless {
        java {
            // TODO: enrich format rules
            removeUnusedImports()
        }
    }
    dependencies {
        implementation 'com.google.guava:guava:29.0-jre'
        implementation platform('io.micrometer:micrometer-bom:1.7.3')
        implementation 'org.slf4j:slf4j-api:1.7.30'
        testImplementation platform("org.junit:junit-bom:${versionMap.junitJupiter}")
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.junit.vintage:junit-vintage-engine'
        testImplementation "org.mockito:mockito-core:${versionMap.mockito}"
        testImplementation "org.mockito:mockito-junit-jupiter:${versionMap.mockito}"
    }
    test {
        useJUnitPlatform()
    }
    build.dependsOn test
    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    task allDeps(type: DependencyReportTask) {}
}

configure(subprojects.findAll {it.name != 'data-prepper-api'}) {
    dependencies {
        implementation platform('com.fasterxml.jackson:jackson-bom:2.12.5')
        implementation platform('software.amazon.awssdk:bom:2.17.15')
        implementation platform('com.amazonaws:aws-java-sdk-bom:1.12.67')
    }
}

configure(coreProjects) {
    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.enabled false
            csv.enabled false
            html.destination file("${buildDir}/reports/jacocoHtml")
        }
    }
    test {
        finalizedBy jacocoTestReport // report is always generated after tests run
    }
    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport
        violationRules {
            rule {
                limit {
                    minimum = 0.65 //TODO increase this to 0.75
                }
            }
        }
    }
    check.dependsOn jacocoTestCoverageVerification
}