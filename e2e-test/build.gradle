/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

subprojects {
    buildscript {
        repositories {
            mavenCentral()
            gradlePluginPortal()
        }
        dependencies {
            classpath 'com.bmuschko:gradle-docker-plugin:7.0.0'
        }
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            resources.srcDir file('src/integrationTest/resources')
        }
    }

    tasks.named('processIntegrationTestResources') {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }

    configurations {
        integrationTestImplementation.extendsFrom testImplementation
        integrationTestRuntime.extendsFrom testRuntime
    }

    def DATA_PREPPER_CORE_JAR = project(':data-prepper-core').jar

    task copyDataPrepperJar(type: Copy) {
        dependsOn DATA_PREPPER_CORE_JAR
        from("${DATA_PREPPER_CORE_JAR.archivePath}")
        into "${project.buildDir}/bin"
    }

    ext {
        dataPrepperJarFilepath = "${project.buildDir.name}/bin/${DATA_PREPPER_CORE_JAR.archiveName}"
    }
}